plugins {
    id 'java'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '5.15.1'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:[5.8.2,)'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:[5.8.2,)'
    testImplementation 'org.hamcrest:hamcrest-core:[2.2,)'
    testImplementation 'org.zapodot:embedded-db-junit-jupiter:[2.0.2,)'
    testImplementation 'org.slf4j:slf4j-api:[1.7.32,)'
    testImplementation 'org.slf4j:slf4j-simple:[1.7.32,)'
}

test {
    useJUnitPlatform()
}

group = 'com.github.javadev'
version = '1.5-SNAPSHOT'
description = 'leetcode-in-java'
java.sourceCompatibility = JavaVersion.VERSION_1_8

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

spotless {
    java {
        encoding 'UTF-8'
        target fileTree(projectDir) {
            include '**/*.java'
            exclude '**/build/**'
        }
        importOrder '\\#', '', '*'
        removeUnusedImports()
        googleJavaFormat('1.7').aosp()
        toggleOffOn()
        endWithNewline()
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
